import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	repositories {
		maven { url = "https://maven.minecraftforge.net" }
		maven { url = "https://repo.spongepowered.org/repository/maven-public" }
	}
	dependencies {
		classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
		classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

def modId = project.modName.toLowerCase()
group = "${project.groupName}.${modId}"
def loadingPlugin = "${group}.${project.modName}LoadingPlugin"
archivesBaseName = project.modName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = "UTF-8"

def copyDir = "$compileJava.temporaryDir/replaced"
task tokenReplace(type: Sync) {
	from(sourceSets.main.java.sourceDirectories)
	into copyDir
	filter(ReplaceTokens, tokens: [
			VERSION: project.version,
			MODNAME: project.modName,
			MODID  : modId,
	])
}
compileJava.setSource(copyDir)
compileJava.dependsOn(tokenReplace)

minecraft {
	mappings channel: "stable", version: "39-1.12"

	runs {
		client {
			workingDirectory project.file("run")

			property "fml.coreMods.load", loadingPlugin
			property "mixin.debug.export", "true"
			property "mixin.debug.verbose", "true"
		}
	}
}

configurations {
	jarLibs
	implementation.extendsFrom jarLibs
}

repositories {
	maven { url = "https://repo.spongepowered.org/repository/maven-public" }
}

dependencies {
	minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2855"

	jarLibs "org.spongepowered:mixin:0.8.3"
	annotationProcessor "org.spongepowered:mixin:0.8.3:processor"
}

mixin {
	add sourceSets.main, "mixins.${modId}.refmap.json"
}

jar {
	manifest.attributes(
			"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
			"FMLCorePluginContainsFMLMod": true,
			"FMLCorePlugin": loadingPlugin,
			"ForceLoadAsMod": true,
	)

	from(configurations.jarLibs.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	inputs.property "modName", project.modName
	inputs.property "modId", modId
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "modName": project.modName, "modId": modId, "version": project.version
	}
}
